const fs = import "../utils/fs"
const lex = import "../utils/lexer"
const string = import "../utils/string"
const std = import "@stdlib"
const mod = import "./mod"

const filename = "./inputs/day01.txt";

struct Input {
	rotations: *mod::Rotation,
	count: int,
}

pub fn parse_input(): Input {
	let file = fs::read_file(&filename);
	std::assert(file.success, "Failed to open file\n");

	let count = string::line_count(file.content);
	let rotations: *mod::Rotation = std::malloc(sizeof(mod::Rotation) * count);

	let lexer = lex::new(file.content, file.length);
	for let i = 0; i < count; i += 1 {
		parse_rotation(&lexer, &rotations[i]);
		lex::consume(&lexer, '\n');
	}

	std::free(file.content);
	return Input { rotations, count, };
}


fn parse_rotation(lexer: *lex::Lexer, rotation: *mod::Rotation) {
	let start = lexer.current;
	match lex::curr(lexer) {
		'L' => rotation.dir = mod::Dir::Left,
		'R' => rotation.dir = mod::Dir::Right,
		_ => {
			std::printf("Expected L/R but got %c", lex::curr(lexer));
		}
	}
	lexer.current += 1;
	rotation.amount = lex::parse_number(lexer);
	let end_temp = lex::curr(lexer);
	if rotation.amount < 0 {
		lexer.content[lexer.current] = '\0';
		std::printf("Rotation amount was negative!\n");
		std::printf("  %s", &lexer.content[start]);
		lexer.content[lexer.current] = end_temp;
		std::exit(1);
	}
}

